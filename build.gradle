// https://docs.spring.io/spring-boot/docs/2.7.9/reference/html/dependency-versions.html#dependency-versions
// https://docs.spring.io/spring-boot/docs/2.7.9/reference/htmlsingle
// https://docs.spring.io/spring-cloud/docs/2021.0.5/reference/html/
buildscript {
    ext {
        springBootVersion = "3.0.2"
        springCloudVersion = "2022.0.1"

        restDocsVersion = "3.0.0"
        snippetsDir = file("$buildDir/generated-snippets")

        kotlinVersion = "1.8.22"
        kotestVersion = "5.6.2"

        h2Version = "1.4.200"
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:3.3.2"

        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "io.kotest:kotest-gradle-plugin:0.3.8"
    }
}

apply plugin: "kotlin"
apply plugin: "kotlin-spring"
apply plugin: "kotlin-jpa"
apply plugin: "kotlin-kapt"
apply plugin: "io.kotest"
apply plugin: "application"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.asciidoctor.jvm.convert"

allOpen {
    annotation("javax.persistence.Entity")
    annotation("javax.persistence.MappedSuperclass")
    annotation("javax.persistence.Embeddable")
}

wrapper {
    gradleVersion = "7.6"
}

group = "io.github.syakuis"
version = "2.0.0-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

compileJava.options.encoding = 'UTF-8'

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

repositories {
    mavenCentral()
}

configurations {
    implementation.exclude module: "spring-boot-starter-tomcat"
    asciidoctorExtensions
}

processResources {
    filesMatching('*.yml') {
        expand(project.properties)
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    kapt "org.springframework.boot:spring-boot-configuration-processor"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-undertow"

    asciidoctorExtensions "org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}"
    api "org.springframework.cloud:spring-cloud-starter-contract-stub-runner"

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    testImplementation "com.h2database:h2:${h2Version}"
    compileOnly "com.h2database:h2:${h2Version}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-test-junit5:${kotlinVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.4"

    testImplementation "io.kotest.extensions:kotest-extensions-spring:1.1.3"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:${kotestVersion}"
    testImplementation "io.kotest:kotest-assertions-core-jvm:${kotestVersion}"
    testImplementation "io.kotest:kotest-property-jvm:${kotestVersion}"
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExtensions'
    dependsOn test
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty 'spring.profiles.active', 'test'
}

application {
    mainClass = "${group}.ApplicationKt"
}

bootJar {
    enabled true
    launchScript()
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into "BOOT-INF/classes/static/api-docs"
    }
}

jar {
    enabled false
}
